buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'gradle.plugin.com.google.cloud.artifactregistry:artifactregistry-gradle-plugin:2.2.0'
    }
}

plugins {
    id 'java-library'
}

apply plugin: 'maven-publish'
apply plugin: 'com.google.cloud.artifactregistry.gradle-plugin'

group project.group
version = project.version
archivesBaseName = project.archivesBaseName

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'
    implementation project(':particleman')
}

jar {
    from sourceSets.main.allSource
    manifest {
        attributes([
                "FMLModType"              : "LIBRARY",
                "Specification-Title"     : "geckolib",
                "Specification-Vendor"    : "geckolib",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "geckolib",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifactId = project.archivesBaseName
            pom {
                name = project.archivesBaseName
                licenses {
                    license {
                        name = 'MIT license'
                        url = 'http://opensource.org/licenses/MIT'
                        distribution = 'repo'
                    }
                }
            }
        }
    }
    repositories {
        repositories {
            maven {
                url "artifactregistry://us-maven.pkg.dev/moonsworth-299m4oir/maven-public"
            }
        }
    }
}
